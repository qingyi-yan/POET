include Loops.incl

<code BasicBlock pars=(label, stmts)>
@label@[@stmts@]
</code>

<xform BuildBasicBlocks pars=(input)>
   blocks = MAP{};
   foreach cur=Nest#(_, body=_) \in input s.t. FALSE do
      blocks[cur] = blocks[body]=1; 
   enddo
   start=""; curBlock = ""; count=0;
   foreach cur=_ \in input s.t. FALSE do
     if (blocks[cur] == 1) {
        if (curBlock != "") {
           blocks[start]=BasicBlock#(count, ReverseList(curBlock));
           count = count + 1; curBlock = "";
        }
        start=cur; 
     }
     if (cur : CODE.Loop|CODE.For|CODE.ExpStmt) { curBlock = cur :: curBlock; }
  enddo
  if (curBlock!="") 
     blocks[start] = BasicBlock#(count+1, curBlock);
  blocks
</xform>

<code GraphEdge pars=(from,to) >
"@(from)@"->"@(to)@"
</code>
<code CFG pars=(label, flow : LIST(GraphEdge,"\n"))>
digraph @label@
{
  @flow@
}
</code>

<xform BuildCFG pars=(input, blocks) prev="" edges="">
    cur=blocks[input]; 
    if (cur != "") 
     { 
       if (prev != "") 
         edges = BuildList(GraphEdge#(prev,cur), edges);
       prev=cur;
     }
   switch (input) {
    case (first=_ second=_): 
       (e1, b1) = BuildCFG(first,blocks);
       if (second != "") { BuildCFG[prev=b1;edges=e1](second,blocks)}
       else { (e1,b1) }
    case Nest#(loop, body):
       (e1,b1) = BuildCFG(body, blocks);
       (BuildList(GraphEdge#(b1,cur),e1), b1)
    case ExpStmt: (edges, prev)
    default: 
       foreach cur=_ \in input s.t. FALSE do 
           if (HEAD(cur) : ExpStmt|Nest) 
              { RETURN (BuildCFG[prev=""](cur,blocks)); }  <<* yes
       enddo
       (edges,prev) <<* no need to look inside
}
</xform>

<parameter infile default="" message="input file name"/>
<parameter outfile default="" message="output file name"/>
<parameter inputLang default="" message="file name for input language syntax" />

<input from=(infile) syntax=(inputLang) to=inputCode/>
<eval blocks=BuildBasicBlocks(inputCode);
      (cfg,_) = BuildCFG(inputCode, blocks); />

<output syntax=(inputLang) to=outfile from=(CFG#("CFG",cfg)) />
